buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
    }
}
apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.af'
version = '1.0.0.8'

repositories {
    jcenter()
    mavenLocal()
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            configuration = configurations.archives
            repository(url: "scp://maven.android-forever.com/var/www/vhosts/pedjaapps.net/maven.android-forever.com") {
                authentication(userName: androidforeverSshUsername, privateKey: androidforeverSshPrivateKey, passphrase: androidforeverSshPassphrase)
            }

            pom.project {
                artifactId 'android-utility'
                name 'android-utility'
                packaging 'aar'
                description 'android-utility'
                url 'http://gitlab.pedjaapps.net/pedja/ts-http'

                /*scm {
                    url 'scm:git@gitlab.pedjaapps.net:pedja/ts-http.git'
                    connection 'scm:git@gitlab.pedjaapps.net:pedja/ts-http.git'
                    developerConnection 'scm:git@gitlab.pedjaapps.net:pedja/ts-http.git'
                }*/
                scm {
                    url 'scm:git@github.com:pedja1/android-utility.git'
                    connection 'scm:git@github.com:pedja1/android-utility.git'
                    developerConnection 'scm:git@github.com:pedja1/android-utility.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'pedja1'
                        name 'Predrag ÄŒokulov'
                    }
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile "com.android.support:recyclerview-v7:23.1.1"
    archives "org.apache.maven.wagon:wagon-ssh:2.2"
}
